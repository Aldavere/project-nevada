#labels Script
#Grenade Hotkey script

{{{
ScriptName PNxFPSGrenadeHotkeyScript

;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
;Variables
;Global PNxFPSGrenadeHotkey             ;Grenade throw hotkey
;Global PNxFPSGrenadeToggleKey          ;key for toggling through grenades

short attackkey                                         
short throwactive                                       
short timerstarted                                     
short curkey                                            
short curkeyB                                          
short keydown                                           
short grenlistcount                             
short listcounter                                     
short SwitchActive                                      
short RealAnimMult

float grentimer                                        

ref grenweapon                                         
ref grentype                                            
ref TempGrenRef                                               


Begin GameMode
;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
;Variable settings
set grentype to listgetnthform PerkLooseCannonWeapons listcounter       ;sets the grenade to the Nth# of the formlist
set grenlistcount to ListGetCount PerkLooseCannonWeapons -1                     ;checks the number of weapons in the grenadeformlist (-1 so the count is right)

if GetAltControl >= 0
	set attackkey to 256 + GetAltControl 4
else 
	set attackkey to GetControl
endif

if(PNxFPSGrenadeToggleKey <=0)
	set PNxFPSGrenadeToggleKey to 35                ;sets default grenadetogglekey to H
endif

if(PNxFPSGrenadeHotkey <=0)
	set PNxFPSGrenadeHotkey to 34                   ;sets default grenadethrowkey to G
endif

;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
;Throw
if  (throwactive==1)                                    
	if (curkey>0 && iskeypressed curkey)    
		set keydown to 1        
	else
		player.removeperk PNxFPSFastEquipPerk
		set timerstarted to 0
		enablekey attackkey
		releasekey attackkey
		set keydown to 0
		set curkey to 0
		set grentimer to 0
		SetWeaponAnimMult RealAnimMult grentype
		if player.getanimaction != 6 && player.getanimaction!=5                                 
			player.EquipItem grenweapon 0 1
			if(player.GetEquippedObject 5==grenweapon)
				set throwactive to 0
			endif
		endif
	endif
endif

;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
;Grenade equip
if (keydown==1)                                                       
    if(timerstarted==0 && player.getanimaction!=0 && player.getequippedobject 5==grenweapon)
		player.Equipitem grentype 0 1   
		set grentimer to 0.1
		set timerstarted to 1
    endif
    if (grentimer>0)
        set grentimer to grentimer - getsecondspassed
    elseif (timerstarted==1 && player.getanimaction!=0 && grentimer <=0) 
        enablekey attackkey
        holdkey attackkey
		if(player.GetEquippedObject 5!=grentype) 
			set curkey to 0
		endif
    endif
endif           

;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
;Hotkey Activation
if (iskeypressed PNxFPSGrenadeHotkey && throwactive==0 && player.getequippedobject 5!=0 && player.getitemcount grentype!=0)
	if(player.getequippedobject 5!=grentype)
		set grenweapon to player.GetEquippedObject 5
	endif
	player.addperk PNxFPSFastEquipPerk
	if GetWeaponAnimMult Grentype !=3
		set RealAnimMult to GetWeaponAnimMult Grentype
	endif
	if GetWeaponskill Grentype == 38
		SetWeaponAnimMult 1.5 grentype
	else
		SetWeaponAnimMult 3 grentype
	endif
	set throwactive to 1
	set curkey to PNxFPSGrenadeHotkey
	disablekey attackkey
endif

;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
;Grenadeswitch Handling
if (player.getitemcount grentype<=0) 
        set SwitchActive to 1
elseif(player.getitemcount grentype>0&&TempGrenRef!=grentype)
        playsound ITMGrenadeDown
        set TempGrenRef to grentype
endif

if (SwitchActive==1)   
	set listcounter to listcounter +1
	if (listcounter>grenlistcount)
		set listcounter to 0
	endif
	set switchactive to 0
endif

if ( curKeyB>0 && isKeyPressed curkeyB) 
	return 
else
	set curkeyB to 0
endif

if ( isKeyPressed PNxFPSGrenadeToggleKey )
	set TempGrenRef to grentype
	set SwitchActive to 1
	set curKeyB to PNxFPSGrenadeToggleKey
endif

end
}}}