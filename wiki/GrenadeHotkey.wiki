#labels Script
#Grenade Hotkey script

{{{
ScriptName PNxCGrenadeHotkeyScript

;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
;Variables
;Global PNxCGrenadeHotkey             ;Grenade throw hotkey
;Global PNxCGrenadeToggleKey          ;key for toggling through grenades

short attackkey
short curkey                                            
short curkeyB
short grenlistcount                             
short listcounter                                                                  
short RealAnimMult
short ammocount

int throwactive                                       
int timerstarted   
int keydown 
int SwitchActive  
int stop

float grentimer                                        

ref grenweapon                                         
ref grentype                                            
ref TempGrenRef 
;ref playerammo                                              


Begin GameMode
;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
;Variable settings
set grentype to listgetnthform GrenadeList listcounter      
set grenlistcount to ListGetCount GrenadeList -1                     

if GetAltControl 4 >= 0
	set attackkey to 256 + GetAltControl 4
else 
	set attackkey to GetControl 4
endif

if throwactive == 0 && stop == 1
	stopquest PNxCGrenadeHotkeyQuest
endif

;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
;Throw
if  (throwactive==1)                                    
	if (curkey>0 && iskeypressed curkey)    
		set keydown to 1        
	else
		player.removeperk PNxCFastEquipPerk
		set timerstarted to 0
		enablekey attackkey
		releasekey attackkey
		set keydown to 0
		set curkey to 0
		set grentimer to 0
		SetWeaponAnimMult RealAnimMult grentype
		if player.getanimaction != 6 && player.getanimaction!=5                                 
			player.EquipItem grenweapon 0 1
			if(player.GetEquippedObject 5==grenweapon)
				setPlayerCurrentAmmoRounds ammocount
				set throwactive to 0
				if stop == 1
					stopquest PNxCGrenadeHotkeyQuest
				endif
			endif
		endif
	endif
endif

;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
;Grenade equip
if (keydown==1)                                                       
    if(timerstarted==0 && player.getanimaction!=0 && player.getequippedobject 5==grenweapon)
		player.Equipitem grentype 0 1   
		set grentimer to 0.3
		set timerstarted to 1
    endif
    if (grentimer>0)
        set grentimer to grentimer - getsecondspassed
    elseif (timerstarted==1 && player.getanimaction!=0 && grentimer <=0) 
        enablekey attackkey
        holdkey attackkey
		if(player.GetEquippedObject 5!=grentype) 
			set curkey to 0
		endif
    endif
endif           

;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
;Hotkey Activation
if (iskeypressed PNxCGrenadeHotkey && throwactive==0 && player.getequippedobject 5!=0 && player.getitemcount grentype!=0)
	if(player.getequippedobject 5!=grentype)
		set grenweapon to player.GetEquippedObject 5
		set ammocount to GetPlayerCurrentAmmoRounds
	endif
	player.addperk PNxCFastEquipPerk
	if GetWeaponAnimMult Grentype !=3
		set RealAnimMult to GetWeaponAnimMult Grentype
	endif
	if GetWeaponskill Grentype == 38
		SetWeaponAnimMult 1.5 grentype
	else
		SetWeaponAnimMult 3 grentype
	endif
	set throwactive to 1
	set curkey to PNxCGrenadeHotkey
	disablekey attackkey
endif

;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
;Grenadeswitch Handling
if (player.getitemcount grentype<=0) 
        set SwitchActive to 1
elseif(player.getitemcount grentype>0&&TempGrenRef!=grentype)
        playsound ITMGrenadeDown
        set TempGrenRef to grentype
endif

if (SwitchActive==1)   
	set listcounter to listcounter +1
	if (listcounter>grenlistcount)
		set listcounter to 0
	endif
	set switchactive to 0
endif

if ( curKeyB>0 && isKeyPressed curkeyB) 
	return 
else
	set curkeyB to 0
endif

if ( isKeyPressed PNxCGrenadeToggleKey )
	set TempGrenRef to grentype
	set SwitchActive to 1
	set curKeyB to PNxCGrenadeToggleKey
endif

end
}}}