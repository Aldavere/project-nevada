{{{
scn PNxCSprintMainScript

short stop

float sprintSpeed
float sprintClock
short sprintCancel
float cooldown

short apTick
float apTimer

float breatheTimer
short breatheTrap

float shudderTimer 

float defaultFOV
float default1stPersonFOV
float curFOV
float nextFOV
short fovTrap

float sensitivity
short sprintKey

short sprintPressed
short attackPressed
short blockPressed
short unarmedskill

short reSneak
short reWalk
short reWeapon
float reWeaponTimer

ref tackleTarget
ref lastTackleTarget 
float tackleAP
float playerTackleCheck
float enemyTackleCheck
float tackleForce

short state


;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
; Event detection
Begin GameMode

; Set sprint key to current run key
if getControl 9 != -1
	set sprintKey to getControl 9
elseif isKeyPressed sprintKey == 0
	setControl 9 sprintKey
endif

; Detect sprint keypress
if isKeyPressed sprintKey == 1
	set sprintPressed to 1
else
	set sprintPressed to 0	
endif

; Detect attack keypress
if isControlPressed 4 == 1
	set attackPressed to 1
else
	set attackPressed to 0	
endif

; Detect block keypress
if isControlPressed 6 == 1
	set blockPressed to 1
else
	set blockPressed to 0	
endif

End


;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
; Breathing sounds
Begin GameMode

if state == 1
	if breatheTrap == 0 && player.getAV actionPoints < player.getBaseAV actionPoints / 4 && breatheTimer <= 0
		set breatheTrap to 1
	endif
endif

if breatheTimer > 0
	set breatheTimer to breatheTimer - getSecondsPassed
else
	if breatheTrap == 1
		if player.getAV ActionPoints < player.getBaseAV ActionPoints / 4
			if getPCIsSex Male == 1
				playSound PNxCSprintBreatheLP
			else
				playSound PNxCSprintBreatheFemaleLP
			endif
			set breatheTimer to 5
		else
			if getPCIsSex Male == 1
				playSound PNxCSprintBreatheEnd
			else
				playSound PNxCSprintBreatheFemaleEnd
			endif
			set breatheTimer to 7
			set breatheTrap to 0
		endif
	endif
endif

End


;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
; HUD shudder
Begin GameMode

;Triggers a HUD shudder regularly while sprinting

if state == 1
	if shudderTimer > 0
		set shudderTimer to shudderTimer - getSecondsPassed
	else
		set shudderTimer to 1
		setStage PNxCUtilShudder 1
	endif
endif

End


;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
; Dynamically change FOV
Begin GameMode

if curFOV != nextFOV

	set fovTrap to 1

	if curFOV > nextFOV
		set curFOV to curFOV - 1
		if curFOV < nextFOV
			set curFOV to nextFOV
		endif
	else
		set curFOV to curFOV + 1
		if curFOV > nextFOV
			set curFOV to nextFOV
		endif
	endif

	setCameraFOV curFOV

	;Changing the FOV also changes this, so we need to change it back to its default
	setNumericIniSetting "fdefault1stpersonfov:display" default1stPersonFOV

else
	set fovTrap to 0
endif

End


;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
; Cooldown
Begin GameMode

if state == 0
	if cooldown >= 0
		set cooldown to cooldown - getSecondsPassed
	endif

	; Prevent sprinting after firing/aiming
	if attackPressed == 1
		set cooldown to 1
	endif
	if blockPressed == 1
		set cooldown to 0.5
	endif
elseif state == 1 && cooldown < 1
	set cooldown to 1
endif

; Prevent inadvertant walking while in cooldown
if player.getAV ActionPoints < apTick || cooldown > 0
	disableControl 9
else
	enableControl 9
endif

End


;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
; Tackle

Begin GameMode

;Tackles actors in front of the player
if state == 1

	;Autoaim enabled (vanilla)
	if getGameSetting fAutoAimScreenPercentage == 10
		set tackleTarget to player.getCombatTarget

	;Autoaim disabled
	else 
		set tackleTarget to player.getCrosshairRef
	endif

	if tackleTarget != 0
		if tackleTarget != lastTackleTarget && tackleTarget.isActor == 1 && tackleTarget.getDead == 0 && player.isInCombat
			set UnarmedSkill to player.getav unarmed
			set tackleAP to (PNxCSprintTackeAPCost - unarmedskill/10)
			if tackleTarget.getDistance player < 100
				set cooldown to 2

				playSound FXMeleeTrapImpact

				setStage PNxCUtilShudder 2

				set playerTackleCheck to player.getAV Strength 
				;if player.getAV ActionPoints < tackleAP
				;	set playerTackleCheck to playerTackleCheck - 10
				;endif
				player.damageAV ActionPoints tackleAP

				set enemyTackleCheck to tackleTarget.getAV Strength

				; player overpowers enemy and enemy has positive AV's
				if playerTackleCheck +2 == enemyTackleCheck 
				
					if sprintClock < 1
						set tackleForce to 4
					elseif sprintClock < 3
						set tackleForce to 8
					else
						set tackleForce to 16
					endif

					if player.hasPerk SuperSlam == 1
						set tackleForce to tackleForce * 2
					endif

					set sprintCancel to 1
					player.pushActorAway tackleTarget tackleForce
					player.pushActorAway player 1

				
				elseif playerTackleCheck + 2 > enemyTackleCheck 

					if sprintClock < 1
						set tackleForce to 4
					elseif sprintClock < 3
						set tackleForce to 8
					else
						set tackleForce to 16
					endif

					if player.hasPerk SuperSlam == 1
						set tackleForce to tackleForce * 2
					endif

					if player.hasPerk UnstoppableForce == 0
						set sprintCancel to 1
					endif

					player.pushActorAway tackleTarget tackleForce

				
				elseif enemyTackleCheck > playerTackleCheck + 2 
					set sprintCancel to 1
					player.pushActorAway player 1

				
;				elseif enemyTackleCheck = 0
;					set sprintCancel to 1
;					player.pushActorAway player 1
				endif

				set lastTackleTarget to tackleTarget
			endif
		endif
	endif
else
	set lastTackleTarget to 0
endif

End


;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
; Main state transitions

Begin GameMode

;--------------------------------------------------------------------------------------------
; Deactivated
if state == 0

	if reWeapon == 1
		if reWeaponTimer > 0
			set reWeaponTimer to reWeaponTimer - getSecondsPassed
		else
			if player.isWeaponOut == 0
				tapControl 4
			endif
			set reWeapon to 0
		endif
	endif

	if stop == 1
		set state to 2
		return
	endif

	if sprintPressed == 1 && player.isMoving == 1 && cooldown <= 0 &&  player.isSwimming == 0 && player.getAV ActionPoints > apTick && getPlayerControlsDisabled 1 0 1 0 1 0 0 == 0 && isControlPressed 6 == 0 && player.getAV LeftMobilityCondition > 0 && player.getAV RightMobilityCondition > 0 && player.getAV InventoryWeight <= player.getAV CarryWeight+1

		; Unbind run
		setControl 9 -1

		;Disable strafing
		disableControl 2 
		disableControl 3

		set sprintSpeed to (player.getAV agility * 8 + 40 * (1 - .75 * player.getAV inventoryWeight / player.getAV carryWeight)) * PNxCSprintSpeedMult
		player.modAV SpeedMult sprintSpeed

		setStage PNxCUtilSpeedRefresh 1

		if fovTrap == 0
			set defaultFOV to GetNumericIniSetting "fDefaultWorldFOV:display"
			set default1stPersonFOV to GetNumericIniSetting "fDefault1stPersonFOV:display"
		endif

		set curFOV to defaultFOV
		set nextFOV to defaultFOV + (sprintSpeed / 10)

		;If the player had their weapon out, schedule re-equip
		if player.isWeaponOut == 1
			set reWeapon to 1
			set reWeaponTimer to 1.1
		endif

		;If the player was sneaking, remember to go back to sneaking later
		if player.isSneaking == 1
			set reSneak to 1
		endif

		disablePlayerControls 0 0 1 0 0 0 1

		set sensitivity to getNumericIniSetting "fMouseSensitivity:controls" 	
		set sensitivity to sensitivity / 3
		setNumericIniSetting "fMouseSensitivity:controls" sensitivity	;increases turning radius

		imod PNxCSprintBlurISFX

		set sprintClock to 0

		set state to 1
		return
	endif

	return

;--------------------------------------------------------------------------------------------
; Activated
elseif state == 1

	set sprintClock to sprintClock + getSecondsPassed

	; Force running if necessary
	if player.isRunning == 0
		tapControl 10
		set reWalk to 1
	endif

	if apTimer > 0
		set apTimer to apTimer - getSecondsPassed
	else

		;Damage the player's action points every 1/10 seconds if they're sprinting
		set apTick to (PNxCSprintAPDrain - player.getAV Endurance) * PNxCSprintAPMult
		set apTick to apTick / 10
		player.damageAV ActionPoints apTick
		set apTimer to 0.1
	endif

	if stop == 1 || sprintPressed == 0 || sprintCancel == 1 || player.isMoving == 0 || player.getAV ActionPoints < apTick / 10 || player.isSwimming == 1 || player.getav LeftMobilityCondition <= 0 || player.getav RightMobilityCondition <= 0 || player.getav InventoryWeight > player.getAV CarryWeight+1 || blockPressed == 1 || getPlayerControlsDisabled 1 0 0 0 1 0 0 == 1
		set sprintCancel to 0

		;Enable strafing
		enableControl 2
		enableControl 3

		set sprintSpeed to sprintSpeed  * -1
		player.modAV SpeedMult sprintSpeed

		setStage PNxCUtilSpeedRefresh 1

		set nextFOV to defaultFOV

		enablePlayerControls 0 0 1 0 0 0 1

		;Re-equip the player's weapon if they had it out before the sprint
		if reWeapon == 1
			tapControl 4
		endif

		;Goes back to sneaking if necessary
		if reSneak == 1
			set reSneak to 0
			tapControl 8	
		endif

		if reWalk == 1
			set reWalk to 0
			tapControl 10
		endif

		set sensitivity to sensitivity * 3
		setNumericIniSetting "fMouseSensitivity:controls" sensitivity ;Returns the turning radius to normal

		rimod PNxCSprintBlurISFX

		set state to 0
		return
	endif

	return

;--------------------------------------------------------------------------------------------
; Disabled
elseif state == 2

	enableControl 9
	set state to 0
	stopquest PNxCSprintMain
	return

endif

End
}}}