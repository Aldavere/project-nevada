#labels Script
= PNxFPSBulletTimeMain =

== Script: ==
{{{

scn PNxFPSBulletTimeMainScript

short stop

float timer
short switchMode

float fActionPointsRestoreRateOld
short iActivatePickLengthOld

short state
short trap

ref target
ref lastTarget

ref weapon

; 0 = no weapon
; 1 = unarmed/fists
; 2 = melee weapon
; 3 = semi auto
; 4 = full auto
short weaponType

short attackCount

float cooldown




short debug

Begin GameMode

if debug == 0
	set debug to 1
	player.additem WeapBaseballBat 1
	player.additem Ammo556mm 500
endif



End

;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
; Event detection

Begin GameMode

if isKeyPressed 38 == 1
	if trap == 0
		set trap to 1
		set switchMode to 1
	endif
else
	set trap to 0
endif

End

;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
; Targeting assistance

Begin GameMode

if state == 2

	set target to getCrosshairRef

	if target != lastTarget

		showMessage PNxFPSBulletTimeDebugMsg

		if target != 0 && target.isActor == 1
			PrintToConsole "Starting shader"
			target.pms PNxFPSBulletTimeTargetFX
		endif

		if lastTarget != 0 && lastTarget.isActor == 1
			PrintToConsole "Stopping shader"
			lastTarget.sms PNxFPSBulletTimeTargetFX
		endif

	endif

	set lastTarget to target

else
	if lastTarget != 0
			lastTarget.sms PNxFPSBulletTimeTargetFX
	endif

	if target != 0
			target.sms PNxFPSBulletTimeTargetFX
	endif

	set target to 0
	set lastTarget to 0
endif

End

;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
; Firing AP drain

Begin GameMode

if state == 2

	if player.isWeaponOut == 0
		; Holstered
		set weaponType to 0
	else
		set weapon to player.getEquippedObject 5

		if weapon == 0
			; Unarmed
			set weaponType to 1
		elseif getWeaponSkill weapon == 38
			; Melee
			set weaponType to 2
		elseif getWeaponIsAutomatic weapon == 0
			; Semi auto
			set weaponType to 3
		else
			; Full auto
			set weaponType to 4
		endif
	endif

	;PrintToConsole "Weapon Type: %.0f" weaponType
 
	; Unarmed
	if weaponType == 1

	; Melee
	elseif weaponType == 2
		if player.getAnimAction == 2
			if trap == 0
				PrintToConsole "Attack detected!"
				set trap to 1
				set attackCount to 1
				showMessage PNxFPSBulletTimeDebug2Msg attackCount
			endif
		else
			set trap to 0
		endif

	; Semi auto
	elseif weaponType == 3



	; Full auto
	elseif weaponType == 4

		if player.getAnimAction == 2
			if cooldown > 0
				set cooldown to cooldown - getSecondsPassed
				printToConsole "Waiting...."
			else
				set cooldown to getNumericGameSetting fVATSShotBurstTime
				set attackCount to 1
				showMessage PNxFPSBulletTimeDebug2Msg attackCount
				printToConsole "attack detected!"

				player.damageAV ActionPoints, 10

			endif
		else
			set cooldown to 0
		endif

	endif
endif

End

;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
; Main state transitions

Begin GameMode

;--------------------------------------------------------------------------------------------
; Inactive - Idle
if state == 0

	if stop == 1
		set state to 4
		return
	endif

	if switchMode == 1
		set switchMode to 0

		set state to 11
		return
	endif

	return

;--------------------------------------------------------------------------------------------
; Activating - Step 1
elseif state == 11

	playsound UIVATSCameraIn
	triggerHitShader 0.5

	set fActionPointsRestoreRateOld to getGameSetting fActionPointsRestoreRate
	setNumericGameSetting fActionPointsRestoreRate, 0

	disableControl 5
	disablePlayerControls 0 1 0 0 0 1 0
	set iActivatePickLengthOld to getGameSetting iActivatePickLength
	setNumericGameSetting iActivatePickLength, 2000

	set state to 12
	return

;--------------------------------------------------------------------------------------------
; Activating -  Step 2
elseif state == 12

	sgtm 0.2

	set timer to 0.5
	imod PNxFPSBulletTimeISFX, 0.5
	set state to 2
	return

;--------------------------------------------------------------------------------------------
; Active - Idle
elseif state == 2

	if timer > 0
		set timer to timer - getSecondsPassed
	else
		set timer to 0.5
	;	player.damageAV ActionPoints, 5
	endif

	; turn off BT if button pressed or AP 0
	if switchMode == 1 || stop == 1 || player.getAV ActionPoints <= 0
		set switchMode to 0
		set state to 3
		return
	endif

	return

;--------------------------------------------------------------------------------------------
; Deactivating
elseif state == 3

	sgtm 1
	playSound UIVATSCameraOut
	triggerHitShader 0.75
	rimod PNxFPSBulletTimeISFX
	setNumericGameSetting  fActionPointsRestoreRate, fActionPointsRestoreRateOld 
	setNumericGameSetting  iActivatePickLength, iActivatePickLengthOld 
	enableControl 5
	enablePlayerControls 0 1 0 0 0 1 0

	if stop == 1
		set state to 4
		return
	endif

	set state to 0
	return

;--------------------------------------------------------------------------------------------
; Disable feature
elseif state == 4

	set state to 0
	set stop to 0

	stopQuest PNxFPSBulletTimeMain
	return

endif

End


;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
Begin MenuMode

;--------------------------------------------------------------------------------------------
if state == 2

	set state to 12
	rimod PNxFPSBulletTimeISFX
	sgtm 1

endif

End

}}}